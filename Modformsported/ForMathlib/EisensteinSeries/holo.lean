import Modformsported.ForMathlib.EisensteinSeries.boundingfunctions
import Mathlib.Analysis.Complex.UpperHalfPlane.Metric
import Mathlib.Analysis.Complex.LocallyUniformLimit
import LeanCopilot

noncomputable section

open ModularForm EisensteinSeries UpperHalfPlane TopologicalSpace Set
  Metric Filter Function Complex   Manifold Pointwise

open scoped Interval Real NNReal ENNReal Topology BigOperators Nat Classical


namespace EisensteinSeries

theorem complex_eisSummand_HasDerivAt(a : Fin 2 â†’ â„¤) (k : â„¤) (z : â„‚) (h : (a 0 : â„‚) * z + a 1 â‰  0) :
    HasDerivAt (fun z : â„‚ => (a 0 * z + a 1) ^ k) (k * (a 0 * z + a 1) ^ (k - 1) * a 0) z := by
  rw [â† Function.comp_def (fun x : â„‚ => x ^ k) ((a 0) * Â· + (a 1))]
  apply HasDerivAt.comp
  Â· exact hasDerivAt_zpow k ((a 0 ) * z + a 1 ) (Or.inl h)
  Â· simpa using (hasDerivAt_id' z).const_mul (a 0 : â„‚) |>.add_const _

lemma comp_inj_ne_zero {Î± Î² Î³: Type*} [OfNat Î² 0] [ OfNat Î³ 0] (f : Î± â†’ Î²) (hf : f â‰  0) (g : Î² â†’ Î³)
    (hg : Injective g) (hg0 : g 0 = 0) : (g âˆ˜ f) â‰  0 := by
  simp only [ne_eq, funext_iff, Pi.zero_apply, not_forall, comp_apply] at hf âŠ¢
  rcases hf with âŸ¨a, haâŸ©
  exact âŸ¨a, by rw [hg.eq_iff'] <;> assumptionâŸ©

example (a : Fin 2 â†’ â„¤) (ha : a â‰  0) : (Int.cast (R := â„)) âˆ˜ a â‰  0 :=
  comp_inj_ne_zero _ ha _ Int.cast_injective Int.cast_zero


lemma complex_eisSummand_differentiableOn (k : â„¤) (a : Fin 2 â†’ â„¤) (hk : k â‰  0) :
  DifferentiableOn â„‚ (fun z : â„‚ => 1/(a 0 * z + a 1) ^ k) (UpperHalfPlane.coe '' âŠ¤) := by
  by_cases ha : a â‰  0
  Â· apply DifferentiableOn.div (differentiableOn_const 1)
    intro z hz
    apply DifferentiableAt.differentiableWithinAt
      (complex_eisSummand_HasDerivAt a k z ?_).differentiableAt
    simp only [ne_eq, top_eq_univ, image_univ, mem_range] at *
    obtain âŸ¨y, hyâŸ© := hz
    rw [â† hy]
    apply UpperHalfPlane.linear_ne_zero ((Int.cast (R := â„)) âˆ˜ a) y
      (comp_inj_ne_zero _ ha _ Int.cast_injective Int.cast_zero)
    intro z hz
    apply zpow_ne_zero
    simp only [top_eq_univ, image_univ, mem_range] at hz
    obtain âŸ¨y, hyâŸ© := hz
    rw [â† hy]
    apply UpperHalfPlane.linear_ne_zero ((Int.cast (R := â„)) âˆ˜ a) y
      (comp_inj_ne_zero _ ha _ Int.cast_injective Int.cast_zero)
  Â· simp only [ne_eq, not_not] at ha
    rw [ha]
    have : ((0 : â„‚)^k)â»Â¹ = 0 := by
      simp only [inv_eq_zero]
      rw [zpow_eq_zero_iff hk]
    simp only [Pi.zero_apply, Int.cast_zero, zero_mul, add_zero, one_div, this, uhc, top_eq_univ,
      image_univ]
    exact differentiableOn_const 0

lemma eisSummad_complex_extension_differentiableOn (k : â„¤) (a : Fin 2 â†’ â„¤) (hk : k â‰  0) :
  DifferentiableOn â„‚ ((fun (z : â„) =>  eisSummand k a z ) âˆ˜
  (PartialHomeomorph.symm
    (OpenEmbedding.toPartialHomeomorph UpperHalfPlane.coe openEmbedding_coe)))
      (UpperHalfPlane.coe '' âŠ¤) := by
  apply DifferentiableOn.congr (complex_eisSummand_differentiableOn k a hk)
  intro z hz
  simp only [eisSummand, one_div, comp_apply, inv_inj]
  have := PartialHomeomorph.left_inv (PartialHomeomorph.symm
    (OpenEmbedding.toPartialHomeomorph UpperHalfPlane.coe openEmbedding_coe)) hz
  simp only [ne_eq, top_eq_univ, image_univ, mem_range, PartialHomeomorph.symm_symm,
    OpenEmbedding.toPartialHomeomorph_apply, UpperHalfPlane.coe] at *
  rw [this]

lemma eisensteinSeries_SIF_complex_extension_differentiableOn (N : â„•) (a: Fin 2 â†’ ZMod N) (k : â„¤)
    (hk : 3 â‰¤ k) : DifferentiableOn â„‚
      ((fun z => SlashInvariantForm.toFun (eisensteinSeries_SIF a k) z) âˆ˜
        â†‘(PartialHomeomorph.symm
          (OpenEmbedding.toPartialHomeomorph UpperHalfPlane.coe openEmbedding_coe)))
      (UpperHalfPlane.coe '' âŠ¤) := by
  have hc := eisensteinSeries_TendstoLocallyUniformlyOn2 hk N a
  let f := ((fun (z : â„) => (eisensteinSeries_SIF a k).1 z)) âˆ˜
  (PartialHomeomorph.symm (OpenEmbedding.toPartialHomeomorph UpperHalfPlane.coe openEmbedding_coe))
  have := @TendstoLocallyUniformlyOn.differentiableOn (E := â„‚) (Î¹ := (Finset â†‘(gammaSet N a))) _ _ _
    (UpperHalfPlane.coe '' âŠ¤) atTop (fun (s : Finset (gammaSet N a )) =>
      (fun (z : â„) => âˆ‘ x in s, eisSummand k x z ) âˆ˜ (PartialHomeomorph.symm
        (OpenEmbedding.toPartialHomeomorph UpperHalfPlane.coe openEmbedding_coe)))
          f (by apply atTop_neBot) hc ((eventually_of_forall fun s => ?_)) ?_
  convert this
  Â· apply DifferentiableOn.sum
    intro v _
    apply eisSummad_complex_extension_differentiableOn
    linarith
  Â· rw [â† OpenEmbedding.open_iff_image_open]
    simp only [top_eq_univ, isOpen_univ]
    exact openEmbedding_coe

theorem Eisenstein_lvl_N_is_mdiff (N : â„•) (a: Fin 2 â†’ ZMod N)  (k : â„¤) (hk : 3 â‰¤ k) :
    MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (fun (z : â„) => (eisensteinSeries_SIF a k).1 z) := by
  rw [MDifferentiable]
  simp only [MDifferentiableAt, differentiableWithinAt_univ, mfld_simps]
  intro z
  have ha : UpperHalfPlane.coe '' âŠ¤ âˆˆ ğ“ â†‘z := by
    apply IsOpen.mem_nhds
    rw [â† OpenEmbedding.open_iff_image_open]
    Â· simp only [top_eq_univ, isOpen_univ]
    Â· exact openEmbedding_coe
    Â· simp only [top_eq_univ, image_univ, mem_range, exists_apply_eq_apply]
  constructor
  rw [PartialHomeomorph.continuousAt_iff_continuousAt_comp_right
    (e := (PartialHomeomorph.symm (OpenEmbedding.toPartialHomeomorph
    UpperHalfPlane.coe openEmbedding_coe)))]
  apply ContinuousOn.continuousAt
    ((eisensteinSeries_SIF_complex_extension_differentiableOn N a k hk).continuousOn)
      (s := (UpperHalfPlane.coe '' âŠ¤)) (x := z) ha
  Â· simp only [PartialHomeomorph.symm_toPartialEquiv, PartialEquiv.symm_target,
    OpenEmbedding.toPartialHomeomorph_source, mem_univ]
  Â· apply DifferentiableOn.differentiableAt (s :=  UpperHalfPlane.coe '' âŠ¤) _ ha
    apply eisensteinSeries_SIF_complex_extension_differentiableOn N a k hk
